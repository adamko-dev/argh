@file:Suppress("UnstableApiUsage")
@file:OptIn(ExperimentalPathApi::class)

import kotlin.io.path.ExperimentalPathApi
import kotlin.io.path.createDirectories
import kotlin.io.path.deleteRecursively
import kotlin.io.path.writeText

plugins {
  buildsrc.`kotlin-gradle-plugin`
}

description = "Argh Gradle plugin for attaching Maven publications as GitHub Release Assets."
mavenPublishing.pomName = "Argh | Gradle Publisher Plugin"

gradlePlugin {
  plugins {
    register("GitHubAssetPublish") {
      id = "dev.adamko.argh.publisher"
      implementationClass = "dev.adamko.argh.gradle.publisher.ArghPublishPlugin"
    }
  }
}

dependencies {
  implementation(projects.modules.libAssetUploaderApi)

  devPublication(projects.modules.libAssetUploader)
  devPublication(projects.modules.libAssetUploaderApi)
  devPublication(projects.modules.libGmm)
  devPublication(projects.modules.libUtils)
  devPublication(projects.modules.libGithubRestClient)
}

testing {
  suites {
    withType<JvmTestSuite>().configureEach {
      targets.configureEach {
        testTask.configure {
          systemProperty("junit.jupiter.tempdir.cleanup.mode.default", "ON_SUCCESS")
        }
      }
    }

    val testIntegration by registering(JvmTestSuite::class) {
      dependencies {
        implementation(gradleTestKit())
        implementation("org.junit.jupiter:junit-jupiter:6.0.1")
        runtimeOnly("org.junit.platform:junit-platform-launcher")
      }

      targets.configureEach {
        testTask.configure {
          val devMavenRepo = devPublish.devMavenRepo
          dependsOn(tasks.updateDevRepo)
          jvmArgumentProviders.add {
            listOf(
              "-DdevMavenRepo=${devMavenRepo.get().asFile.invariantSeparatorsPath}"
            )
          }
        }
      }
    }
    tasks.check { dependsOn(testIntegration) }
  }
}

val generateBuildConstants by tasks.registering {
  group = project.name

  val outputDir = temporaryDir.toPath()
  outputs.dir(outputDir).withPropertyName("outputDir")
  outputs.cacheIf { true }

//  val libGmmRewriterCoords = providers.provider {
//    projects.modules.libGmmRewriter.run { "$group:$name:$version" }
//  }
//  inputs.property("libGmmRewriterCoords", libGmmRewriterCoords)
  val libAssetUploaderCoords = providers.provider {
    projects.modules.libAssetUploader.run { "$group:$name:$version" }
  }
  inputs.property("libAssetUploaderCoords", libAssetUploaderCoords)

  val taskPath = providers.provider { path }
  inputs.property("taskPath", taskPath)

  doLast {
    outputDir.deleteRecursively()
    outputDir.resolve("BuildConstants.kt").apply {
      parent.createDirectories()
      writeText(
        """
        |// DO NOT EDIT - generated by ${taskPath.get()}
        |@file:Suppress("ConstPropertyName")
        |
        |package dev.adamko.argh.gradle.publisher.internal
        |
        |internal object BuildConstants {
        |  const val libAssetUploaderCoords: String = "${libAssetUploaderCoords.get()}"
        |}
        |""".trimMargin()
      )
    }
  }
}

kotlin.sourceSets.main {
  kotlin.srcDir(generateBuildConstants)
}
